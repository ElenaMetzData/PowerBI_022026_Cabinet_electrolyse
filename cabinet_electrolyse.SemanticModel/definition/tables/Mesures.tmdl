table Mesures
	lineageTag: 5ec9c2b6-cbae-403a-8412-70ec3be84bf5

	///  Chiffre d'affaire dans le contexte de filtre
	measure CA =
			
			CALCULATE(
			    SUM(planity_rdv_36_mois[montant_recu_eur])
			)
		formatString: #,0.###############\ "€";-#,0.###############\ "€";#,0.###############\ "€"
		lineageTag: cbb77961-fb1e-4d61-a9c9-c3adf5604335

		annotation PBI_FormatHint = {"currencyCulture":"fr-FR"}

	/// Nom du dernier mois civil terminé
	measure 'Titre Dernier Mois Terminé' =
			
			FORMAT(
			    [Date Dernier Mois Terminé],
			    "mmmm"
			)
		lineageTag: c598f2ad-419c-4d86-b7e3-309fee3dfaf3

	/// Chiffre d'affaire de l'avant-dernier mois civil
	measure 'CA M-2' =
			--- Chiffre d'affaire du dernier mois civil terminé 1 an plus tôt (ignore le contexte de filtre)
			VAR EndDate   = EOMONTH(TODAY(), -2)
			VAR StartDate = DATE(YEAR(EndDate), MONTH(EndDate), 1)
			RETURN
			CALCULATE(
			    [CA],
			    DATESBETWEEN(Calendrier[Date], StartDate, EndDate)
			)
		formatString: #,0.###############\ "€";-#,0.###############\ "€";#,0.###############\ "€"
		lineageTag: dc94ecf0-30ca-4149-a2fe-0744e3fd2d27

		annotation PBI_FormatHint = {"currencyCulture":"fr-FR"}

	/// Chiffre d'affaire du dernier mois civil terminé 1 an plus tôt
	measure 'CA M-1 N-1' =
			
			VAR EndDate   = EOMONTH(TODAY(), -13)
			VAR StartDate = DATE(YEAR(EndDate), MONTH(EndDate), 1)
			RETURN
			CALCULATE(
			    [CA],
			    DATESBETWEEN(Calendrier[Date], StartDate, EndDate)
			)
		formatString: #,0.###############\ "€";-#,0.###############\ "€";#,0.###############\ "€"
		lineageTag: 38590d18-ef3d-4ac6-bf14-d60a661cca01

		annotation PBI_FormatHint = {"currencyCulture":"fr-FR"}

	/// Variation en pourcentage du CA entre l'avant-dernier moi civil et le dernier mois civil terminé.
	measure 'Var % MoM (ignore filtres)' =
			
			DIVIDE(
			    [CA M-1 (ignore filtres)] - [CA M-2 (ignore filtres)],
			    [CA M-2 (ignore filtres)]
			)
		lineageTag: 13fa3000-11c7-49ec-8f74-f507a21470d9

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Variation en pourcentage du CA entre le dernier mois civil terminé civil 12 mois plus tôt et le dernier mois civil terminé.
	measure 'Var % YoY (ignore filtres)' =
			
			DIVIDE(
			    [CA M-1 (ignore filtres)] - [CA M-1 N-1 (ignore filtres)],
			    [CA M-1 N-1 (ignore filtres)]
			)
		lineageTag: 8707c1ad-931a-486c-ac0c-02427016dc98

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Chiffre d'affaire du dernier mois civil terminé (ignore contexte de filtre)
	measure 'CA M-1 (ignore filtres)' =
			
			VAR EndDate   = [Date Fin M-1 (today)]
			VAR StartDate = DATE(YEAR(EndDate), MONTH(EndDate), 1)
			RETURN
			CALCULATE(
			    [CA],
			    REMOVEFILTERS(Calendrier),
			    DATESBETWEEN(Calendrier[Date], StartDate, EndDate)
			)
		formatString: #,0.###############\ "€";-#,0.###############\ "€";#,0.###############\ "€"
		lineageTag: c5ac98f4-f4d9-4bd8-b7b9-85d3c633ac52

		annotation PBI_FormatHint = {"currencyCulture":"fr-FR"}

	/// Date du dernier jour du dernier moi civil terminé
	measure 'Date Fin M-1 (today)' =
			
			EOMONTH(TODAY(), -1)
		formatString: General Date
		lineageTag: 1ec3014f-eb03-495a-9e26-a4b5543339d2

	/// Date du premier jour du dernier moi civil terminé
	measure 'Date Début M-1 (today)' =
			
			VAR EndDate = [Date Fin M-1 (today)]
			RETURN DATE(YEAR(EndDate), MONTH(EndDate), 1)
		formatString: General Date
		lineageTag: 0f9f6aac-489c-4137-b902-10097ab3c752

	/// "+/- x% par rapport à M-1", soit l'évolution en pourcentage du chiffre d'ffaire du dernier moi civil terminé par rapport à l'avant-dernier mois civil
	measure 'Sous-titre Var MoM' =
			
			VAR VarMoM = [Var % MoM (ignore filtres)]
			VAR Sign = IF(VarMoM > 0, "+", "")
			VAR TxtPct =
			    IF(
			        ISBLANK(VarMoM),
			        "—",
			        Sign & FORMAT(VarMoM, "0%")
			    )
			RETURN
			IF(
			    ISBLANK(VarMoM),
			    "Pas de comparaison disponible",
			    TxtPct & " VS M-1")
		lineageTag: 959792f6-2004-475d-9a8f-5f9ae2106f0a

	/// "+/- x% par rapport à Y-1", soit l'évolution en pourcentage du chiffre d'ffaire du dernier moi civil terminé par rapport au même mois de la dernière année civile
	measure 'Sous-titre Var YoY' =
			
			VAR VarYoY = [Var % YoY (ignore filtres)]
			VAR Sign = IF(VarYoY > 0, "+", "")
			VAR TxtPct =
			    IF(
			        ISBLANK(VarYoY),
			        "—",
			        Sign & FORMAT(VarYoY, "0%")
			    )
			RETURN
			IF(
			    ISBLANK(VarYoY),
			    "Pas de comparaison Y-1 disponible",
			    TxtPct & " par rapport à Y-1"
			)
		lineageTag: db5e8677-1196-4cef-bd0f-c5d2a121e5b0

	/// Total des revenus apparaissant sur le compte bancaire dans le contexte de filtre
	measure Paiements =
			CALCULATE(
			    SUM(banque_transactions_36_mois[montant_eur]),
			    banque_transactions_36_mois[type] = "Recette"
			)
		formatString: #,0.###############\ "€";-#,0.###############\ "€";#,0.###############\ "€"
		lineageTag: 1de25ef5-c899-4be8-84c5-8adc239eb1e5

		annotation PBI_FormatHint = {"currencyCulture":"fr-FR"}

	/// Total des charges apparaissant sur le compte bancaire professionnel dans le contexte de filtre
	measure Charges =
			
			ABS(
			    CALCULATE(
			        SUM(banque_transactions_36_mois[montant_eur]),
			        banque_transactions_36_mois[type] = "Charge"
			    )
			)
		formatString: #,0.###############\ "€";-#,0.###############\ "€";#,0.###############\ "€"
		lineageTag: f0b7b65c-0954-4b1d-96fb-f01bc5088332

		annotation PBI_FormatHint = {"currencyCulture":"fr-FR"}

	/// Résultat net des opérations sur le compte bancaire professionnel
	measure 'Résultat net' = [Paiements]-[Charges]
		formatString: #,0.###############\ "€";-#,0.###############\ "€";#,0.###############\ "€"
		lineageTag: 15b6ea8b-3740-4599-8a49-0c5f85f72dc8

		annotation PBI_FormatHint = {"currencyCulture":"fr-FR"}

	/// Nombre d'heures de prestation réalisées (hors temps administratif et temps sans client)
	measure 'Heures RDV réalisés' =
			
			DIVIDE(
			    CALCULATE(
			        SUM(planity_rdv_36_mois[duree_prestation_min]),
			        planity_rdv_36_mois[statut] IN {"Réalisé","Honoré"}
			    ),
			    60
			)
		lineageTag: beebefd2-93aa-4f0e-9af9-96a8a55adb81

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Total d'heures d'ouverture théoriques du cabinet dans le contexte de filtre (hors congés, jours fériés, fermetures exceptionnelles)
	measure 'Heures ouverture théoriques' =
			
			SUMX(
			    VALUES(Calendrier[Date]),
			    VAR dow = WEEKDAY(Calendrier[Date], 2)  -- 1=Lun ... 7=Dim
			    RETURN
			        SWITCH(
			            TRUE(),
			            dow >= 1 && dow <= 5, 10,
			            dow = 6, 5,
			            0
			        )
			)
		formatString: 0
		lineageTag: 516b9257-ec3b-4e31-abbb-99a4ebd5dd42

	/// Taux de remplissage par rapport aux heures d'ouverture théoriques
	measure 'Taux de remplissage' =
			
			DIVIDE([Heures RDV réalisés], [Heures ouverture théoriques])
		lineageTag: 43f9c6c3-a7f3-42b1-892a-6717431fb694

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Nombre de clients uniques ayant effectué au moins un RDV réalisé ou honoré au cours des 6 derniers mois glissants.
	measure 'Clients actifs (6M)' =
			
			VAR DateFin = MAX(Calendrier[Date])
			VAR DateDebut = EDATE(DateFin, -6)
			RETURN
			CALCULATE(
			    DISTINCTCOUNT(planity_rdv_36_mois[client_id]),
			    planity_rdv_36_mois[statut] IN {"Réalisé","Honoré"},
			    DATESBETWEEN(Calendrier[Date], DateDebut, DateFin)
			)
		formatString: 0
		lineageTag: a5e7fceb-2efc-47c0-893c-8a9cd58a7f26

	/// Nombre de nouveau clients enregistrés sur le dernier mois civil terminé
	measure 'Nouveaux clients du mois' =
			
			VAR MoisDebut = DATE(YEAR(MAX(Calendrier[Date])), MONTH(MAX(Calendrier[Date])), 1)
			VAR MoisFin = EOMONTH(MAX(Calendrier[Date]), 0)
			RETURN
			CALCULATE(
			    DISTINCTCOUNT(planity_rdv_36_mois[client_id]),
			    FILTER(
			        VALUES(planity_rdv_36_mois[client_id]),
			        VAR FirstVisit =
			            CALCULATE(
			                MIN(planity_rdv_36_mois[date]),
			                planity_rdv_36_mois[statut] IN {"Réalisé","Honoré"},
			                ALL(Calendrier)
			            )
			        RETURN FirstVisit >= MoisDebut && FirstVisit <= MoisFin
			    )
			)
		formatString: 0
		lineageTag: 67689c97-5459-49f3-bf51-26c9e5ada81d

	/// Chiffre d'affaire de l'avant-dernier mois civil (ignore le contexte de filtre)
	measure 'CA M-2 (ignore filtres)' =
			
			VAR EndDate   = EOMONTH(TODAY(), -2)
			VAR StartDate = DATE(YEAR(EndDate), MONTH(EndDate), 1)
			RETURN
			CALCULATE(
			    [CA],
			    REMOVEFILTERS(Calendrier),
			    DATESBETWEEN(Calendrier[Date], StartDate, EndDate)
			)
		formatString: #,0.###############\ "€";-#,0.###############\ "€";#,0.###############\ "€"
		lineageTag: 1045c4f6-f2b0-47b5-8c59-7f0736557aae

		annotation PBI_FormatHint = {"currencyCulture":"fr-FR"}

	/// Chiffre d'affaire du dernier mois civil terminé 1 an plus tôt (ignore le contexte de filtre)
	measure 'CA M-1 N-1 (ignore filtres)' =
			--- Chiffre d'affaire du dernier mois civil terminé 1 an plus tôt
			VAR EndDate   = EOMONTH(TODAY(), -13)
			VAR StartDate = DATE(YEAR(EndDate), MONTH(EndDate), 1)
			RETURN
			CALCULATE(
			    [CA],
			    REMOVEFILTERS(Calendrier),
			    DATESBETWEEN(Calendrier[Date], StartDate, EndDate)
			)
		formatString: #,0.###############\ "€";-#,0.###############\ "€";#,0.###############\ "€"
		lineageTag: 233fa1d5-58e6-4091-b35f-8627daa56626

		annotation PBI_FormatHint = {"currencyCulture":"fr-FR"}

	/// Valeur de vie client calculée sur les 12 premiers mois après la première consultation. Seuls les clients ayant réalisés leur première consultation 12 mois auparavant minimum sont pris en compte. Q : combien de temps max dure un traitement ? certains clients font-ils plusieurs zones ?
	measure 'LTV estimée (12 mois après 1re consult)' =
			
			VAR Pivot = [Date Pivot (mois terminé)]
			VAR ClientsCohorte = VALUES(clients_36_mois[client_id])
			RETURN
			AVERAGEX(
			    FILTER(
			        ClientsCohorte,
			        VAR DateStart =
			            CALCULATE(
			                MIN(clients_36_mois[date_premiere_consultation])
			            )
			        VAR DateEnd = EDATE(DateStart, 12)
			        RETURN NOT ISBLANK(DateStart) && DateEnd <= Pivot
			    ),
			    VAR DateStart =
			        CALCULATE(
			            MIN(clients_36_mois[date_premiere_consultation])
			        )
			    VAR DateEnd = EDATE(DateStart, 12)
			    RETURN
			        CALCULATE(
			            SUM(planity_rdv_36_mois[montant_recu_eur]),
			            planity_rdv_36_mois[statut] IN {"Réalisé","Honoré"},
			            REMOVEFILTERS(Calendrier),
			            DATESBETWEEN(Calendrier[Date], DateStart, DateEnd)
			        )
			)
		formatString: 0
		lineageTag: 99d56178-0860-40c5-99b2-fc1ae8a64bb5

	/// Durée moyenne de suivi d’un client, en mois, entre son premier et son dernier rendez-vous réalisé, en ignorant les filtres de date.
	measure 'Durée traitement moyenne (mois) (ignore filtres)' =
			
			VAR T =
			    ADDCOLUMNS(
			        VALUES(clients_36_mois[client_id]),
			        "FirstDate",
			            CALCULATE(
			                MIN(planity_rdv_36_mois[date]),
			                REMOVEFILTERS(Calendrier),
			                planity_rdv_36_mois[statut] IN {"Réalisé","Honoré"}
			            ),
			        "LastDate",
			            CALCULATE(
			                MAX(planity_rdv_36_mois[date]),
			                REMOVEFILTERS(Calendrier),
			                planity_rdv_36_mois[statut] IN {"Réalisé","Honoré"}
			            )
			    )
			RETURN
			AVERAGEX(
			    FILTER(T, NOT(ISBLANK([FirstDate])) && NOT(ISBLANK([LastDate]))),
			    DIVIDE(DATEDIFF([FirstDate], [LastDate], DAY), 30.4375)
			)
		lineageTag: b62b0dd4-5d52-48a0-97f8-fa1e639eca01

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Dernière date du dernier mois civil terminé
	measure 'Date Dernier Mois Terminé' = EOMONTH(TODAY(), -1)
		formatString: General Date
		lineageTag: 0d214e82-bda0-4365-b257-5617670448b4

	/// Heures de RDV annulés, ou le/la client.e ne s'est pas présenté.e
	measure 'Heures RDV annulés' = ```
			
			DIVIDE(
			    CALCULATE(
			        SUM(planity_rdv_36_mois[duree_prestation_min]),
			        planity_rdv_36_mois[statut] = "Annulé"
			    ),
			    60
			)
			
			```
		lineageTag: cebbf1ea-810a-4c08-bb6f-eaa96977d2cd

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Nombre d'heures de soins effectifs pour lesquelles le/la client.es ne s'est pas présenté.e
	measure 'Heures RDV no-show' =
			
			DIVIDE(
			    CALCULATE(
			        SUM(planity_rdv_36_mois[duree_prestation_min]),
			        planity_rdv_36_mois[statut] = "No-show"
			    ),
			    60
			)
		lineageTag: a325bf28-89b0-4159-8951-f87a73a7ff13

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Heures non facturées (admin)' =
			
			DIVIDE(
			    CALCULATE(
			        SUM(planity_rdv_36_mois[temps_non_facture_min])
			    ),
			    60
			)
		lineageTag: dc44939f-8417-43a6-bca8-eeed8d02fbd3

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Taux d'annulation par rapport aux heures d'ouverture théoriques
	measure 'Taux annulation' =
			
			DIVIDE([Heures RDV annulés], [Heures ouverture théoriques])
		lineageTag: c07f0a19-04dc-4446-9ea0-1fbb5de35165

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Taux de remplissage du dernier mois civil terminé par rapport aux heures d'ouverture théoriques (ignore le contexte de filtre)
	measure 'Taux remplissage M-1 (ignore filtres)' =
			
			VAR EndDate   = [Date Fin M-1 (today)]
			VAR StartDate = DATE(YEAR(EndDate), MONTH(EndDate), 1)
			RETURN
			CALCULATE(
			    [Taux de remplissage],
			    REMOVEFILTERS(Calendrier),
			    DATESBETWEEN(Calendrier[Date], StartDate, EndDate)
			)
		lineageTag: d73e6fb6-8e31-42cf-970e-4bb86878d1f1

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Heures RDV réalisés - 12M' =
			
			VAR Pivot = [Date Pivot (mois terminé)]
			RETURN
			CALCULATE(
			    [Heures RDV réalisés],
			    DATESINPERIOD(Calendrier[Date], Pivot, -12, MONTH)
			)
		lineageTag: 293a14a3-fea9-47c9-974e-528404bbeb5e

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Heures RDV (réel + prévision)' =
			
			VAR d = MAX(Calendrier[Date])
			VAR DatePivot = MAXX(ALLSELECTED(Calendrier[Date]), Calendrier[Date])
			RETURN
			IF(
			    d <= DatePivot,
			    [Heures RDV réalisés],
			    [Heures RDV réalisés - 12M] / 7   -- converti en "par jour" si ton axe est quotidien
			)
		isHidden
		lineageTag: 304efe1e-4139-471c-9dfc-ea3e4e481c83

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Chiffre d'affaire du dernier mois civil terminé
	measure 'CA M-1' =
			
			VAR EndDate   = [Date Fin M-1 (today)]
			VAR StartDate = DATE(YEAR(EndDate), MONTH(EndDate), 1)
			RETURN
			CALCULATE(
			    [CA],
			    DATESBETWEEN(Calendrier[Date], StartDate, EndDate)
			)
		formatString: #,0.###############\ "€";-#,0.###############\ "€";#,0.###############\ "€"
		lineageTag: d3f8df39-320a-4d54-ae74-5bd42b2501f5

		annotation PBI_FormatHint = {"currencyCulture":"fr-FR"}

	/// Date actuelle (pour date pivot)
	measure 'Date Pivot' =
			
			MAXX(ALLSELECTED(Calendrier[Date]), Calendrier[Date])
		formatString: General Date
		lineageTag: 788e0821-9ebf-4d46-a7f9-7ff31585ea9c

	/// Nombre d'heures de soins effectifs calées à partir de la date pivot
	measure 'Heures RDV calées' =
			
			VAR Pivot = [Date Pivot]
			RETURN
			CALCULATE(
			    SUM(planity_rdv_36_mois[durée_prestation_heure]),            -- ou ta mesure d'heures
			    planity_rdv_36_mois[date] > Pivot,
			    planity_rdv_36_mois[statut] IN {"Confirmé","Planifié","À venir"}
			)
		formatString: 0
		lineageTag: 41095596-3d4e-4be1-8c0b-047deab2c51c

	/// Facteur de saisonnalité calculé sur 1 an, sensibilité à la semaine
	measure 'Facteur saisonnalité (semaine) - lissé' =
			
			VAR Pivot = [Date Pivot (mois terminé)]
			VAR WeekNum = MAX(Calendrier[Numero de la semaine])
			VAR Window52W =
			    DATESINPERIOD(Calendrier[Date], Pivot, -364, DAY)
			VAR Heures3Weeks =
			    CALCULATE(
			        [Heures RDV réalisés],
			        FILTER(
			            Window52W,
			            VAR w = WEEKNUM(Calendrier[Date], 21)
			            RETURN w IN { WeekNum - 1, WeekNum, WeekNum + 1 }
			        )
			    )
			VAR AvgWeekly =
			    DIVIDE(
			        CALCULATE([Heures RDV réalisés], Window52W),
			        52
			    )
			RETURN
			DIVIDE(Heures3Weeks / 3, AvgWeekly)
		lineageTag: 7c9463d1-7d95-4fde-bfef-0961631dd16b

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Tendance du nombre de nouveaux clients sur les 12 derniers mois
	measure 'Nouveaux clients - moy / mois (12M)' =
			
			VAR Pivot = [Date Pivot (mois terminé)]
			RETURN
			AVERAGEX(
			    DATESINPERIOD(Calendrier[Date], Pivot, -12, MONTH),
			    [Nouveaux clients du mois]
			)
		lineageTag: d2836afa-58e9-4d9e-80b1-579611aca3c1

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Calcul de l'évolution mensuelle du taux d'abandon au cours de l'année dernière glissante
	measure 'Taux abandon - moy mensuel (12M)' =
			
			VAR Pivot = [Date Pivot (mois terminé)]
			RETURN
			AVERAGEX(
			    DATESINPERIOD(Calendrier[Date], Pivot, -12, MONTH),
			    VAR FinM = EOMONTH(MAX(Calendrier[Date]), 0)
			    VAR DebM = DATE(YEAR(FinM), MONTH(FinM), 1)
			    VAR DebM1 = EOMONTH(FinM, 0) + 1
			    VAR FinM1 = EOMONTH(FinM, 1)
			
			    VAR ActifsM =
			        CALCULATETABLE(
			            VALUES(planity_rdv_36_mois[client_id]),
			            planity_rdv_36_mois[statut] IN {"Réalisé","Honoré"},
			            DATESBETWEEN(Calendrier[Date], DebM, FinM)
			        )
			    VAR ActifsM1 =
			        CALCULATETABLE(
			            VALUES(planity_rdv_36_mois[client_id]),
			            planity_rdv_36_mois[statut] IN {"Réalisé","Honoré"},
			            DATESBETWEEN(Calendrier[Date], DebM1, FinM1)
			        )
			    VAR Retenus = COUNTROWS(INTERSECT(ActifsM, ActifsM1))
			    VAR Total = COUNTROWS(ActifsM)
			    RETURN 1 - DIVIDE(Retenus, Total)
			)
		lineageTag: 5cbfcd8d-7c13-41b2-af47-45652fe4731b

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Heures de soins réalisées par client actifs au cours des 12 derniers mois
	measure 'Heures par client actif (12M)' =
			
			VAR Pivot = [Date Pivot (mois terminé)]
			VAR Heures = [Heures RDV réalisés - 12M]
			VAR ClientsActifs =
			    CALCULATE(
			        DISTINCTCOUNT(planity_rdv_36_mois[client_id]),
			        planity_rdv_36_mois[statut] IN {"Réalisé","Honoré"},
			        DATESINPERIOD(Calendrier[Date], Pivot, -12, MONTH)
			    )
			RETURN
			DIVIDE(Heures, ClientsActifs)
		lineageTag: d3b4b975-aa90-4204-9ab6-46c30b240c6a

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Nombre de clients actifs projetés sur les 6 prochains mois (sont pris en compte : les rdv planifiés, la taux d'abandon et les nouveaux clients, et la saisonnalité)
	measure 'Clients actifs projetés' =
			
			VAR Pivot = [Date Pivot (mois terminé)]
			VAR d = MAX(Calendrier[Date])
			VAR N = MAX(0, DATEDIFF(EOMONTH(Pivot,0), EOMONTH(d,0), MONTH))
			
			VAR ActifsNow =
			    CALCULATE(
			        DISTINCTCOUNT(planity_rdv_36_mois[client_id]),
			        planity_rdv_36_mois[statut] IN {"Réalisé","Honoré"},
			        DATESINPERIOD(Calendrier[Date], Pivot, -1, MONTH)
			    )
			VAR NewAvg = [Nouveaux clients - moy / mois (12M)]
			VAR Churn = [Taux abandon - moy mensuel (12M)]
			VAR Ret = 1 - Churn
			RETURN
			IF(
			    Churn = 0,
			    ActifsNow + NewAvg * N,
			    ActifsNow * POWER(Ret, N) + NewAvg * (1 - POWER(Ret, N)) / Churn
			)
		lineageTag: c7ff5c19-daff-49fd-b768-faf49734a46c

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Heures attendues non calées sur la base des 12 derniers mois, en prenant en compte les rdv prévus et la saisonnalité.
	measure 'Heures attendues (non calées)' =
			
			[Clients actifs projetés] * [Heures par client actif (12M)] * [Facteur saisonnalité (semaine) - lissé]
		lineageTag: be4570b2-9b16-4afe-9d1f-59763de718db

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Heures réellement réalisées dans le passé et, pour le futur, rendez-vous déjà planifiés avec une estimation des heures supplémentaires attendues
	measure 'Heures RDV (réel + calés + prévision)' =
			
			VAR Pivot = [Date Pivot (mois terminé)]
			VAR d = MAX(Calendrier[Date])
			RETURN
			IF(
			    d <= Pivot,
			    [Heures RDV réalisés],
			    [Heures RDV calées] + [Heures attendues (non calées)]
			)
		lineageTag: b0b5b8a3-4319-4cfd-8f4e-5d5ad80e0448

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'CA moyen par client (réalisé)' =
			
			DIVIDE(
			    [CA],
			    CALCULATE(
			        DISTINCTCOUNT(planity_rdv_36_mois[client_id]),
			        planity_rdv_36_mois[statut] IN {"Réalisé","Honoré"}
			    )
			)
		formatString: #,0.###############\ "€";-#,0.###############\ "€";#,0.###############\ "€"
		isHidden
		lineageTag: 17687465-adff-43f9-a1f1-9688ad869a96

		annotation PBI_FormatHint = {"currencyCulture":"fr-FR"}

	measure 'CA moyen mensuel par client (réalisé)' =
			
			DIVIDE(
			    CALCULATE([CA], planity_rdv_36_mois[statut] IN {"Réalisé","Honoré"}),
			    CALCULATE(
			        DISTINCTCOUNT(planity_rdv_36_mois[client_id]),
			        planity_rdv_36_mois[statut] IN {"Réalisé","Honoré"}
			    )
			)
		formatString: #,0.###############\ "€";-#,0.###############\ "€";#,0.###############\ "€"
		isHidden
		lineageTag: fedcaeb4-b81d-4b4b-aa1e-4e34e1fc89d9

		annotation PBI_FormatHint = {"currencyCulture":"fr-FR"}

	measure 'LTV estimée (CA)' =
			
			[CA moyen mensuel par client (réalisé)] * [Durée traitement moyenne (mois) (ignore filtres)]
		formatString: #,0.###############\ "€";-#,0.###############\ "€";#,0.###############\ "€"
		isHidden
		lineageTag: 30d30c27-7cb8-489b-88e2-02fa224da0a2

		annotation PBI_FormatHint = {"currencyCulture":"fr-FR"}

	/// Taux d'heures occupées mais non facturées (temps d'échange, administratif) par rapport aux heures d'ouverture théoriques
	measure 'Taux d''heures non facturées (admin)' =
			
			DIVIDE([Heures non facturées (admin)], [Heures ouverture théoriques])
		lineageTag: bab1e660-3168-420c-912e-a37a337cd8e1

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Canal d''acquisition - Site web' =
			
			CALCULATE(
			    DISTINCTCOUNT(clients_36_mois[client_id]),
			    clients_36_mois[canal_acquisition] = "Site internet"
			)
		formatString: 0
		lineageTag: 37021c3b-7e76-408c-9eae-b4b22ceadd89

	measure 'Canal d''acquisition - Bouche-à-oreille' =
			
			CALCULATE(
			    DISTINCTCOUNT(clients_36_mois[client_id]),
			    clients_36_mois[canal_acquisition] = "Bouche-à-oreille"
			)
		formatString: 0
		lineageTag: eded56e9-d752-4090-aca2-e2e3853aa072

	measure 'Canal d''acquisition - Instagram' =
			
			CALCULATE(
			    DISTINCTCOUNT(clients_36_mois[client_id]),
			    clients_36_mois[canal_acquisition] = "Instagram"
			)
		formatString: 0
		lineageTag: b7a75742-42e3-440f-93a4-244b1ed8585f

	measure 'Canal d''acquisition - médecin' =
			
			CALCULATE(
			    DISTINCTCOUNT(clients_36_mois[client_id]),
			    clients_36_mois[canal_acquisition] = "Médecin dermatologue"
			)
		formatString: 0
		lineageTag: a6f3d246-d385-485f-aed9-a82e8c935683

	measure 'Canal d''acquisition - Planity' =
			
			CALCULATE(
			    DISTINCTCOUNT(clients_36_mois[client_id]),
			    clients_36_mois[canal_acquisition] = "Planity"
			)
		formatString: 0
		lineageTag: 896c38ad-5db5-4a97-a37e-8ad4caa2508e

	measure 'Fenêtre Graph Remplissage (12M + 6M)' =
			
			VAR Pivot = [Date Pivot (mois terminé) (mois terminé)]
			VAR MoisDebut = EOMONTH(Pivot, -12) + 1
			VAR MoisFin   = EOMONTH(Pivot, 6)
			VAR d = MAX(Calendrier[Date])
			RETURN
			IF(d >= MoisDebut && d <= MoisFin, 1, 0)
		formatString: 0
		lineageTag: 7924fac3-d747-4639-ac4a-2b3a95998ded

	measure 'Tx remplissage - Réel (mensuel)' =
			
			VAR Pivot = [Date Pivot (mois terminé) (mois terminé)]
			VAR d = MAX(Calendrier[Date])
			RETURN
			IF(
			    d <= Pivot,
			    [Taux de remplissage],
			    BLANK()
			)
		lineageTag: 26b0cc11-598b-40bb-af08-9a3188084b27

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Tx remplissage - Prévision (6M, mensuel)' =
			
			VAR Pivot = [Date Pivot (mois terminé) (mois terminé)]
			VAR d = MAX(Calendrier[Date])
			RETURN
			IF(
			    d > Pivot && d <= EOMONTH(Pivot, 6),
			    DIVIDE(
			        [Heures RDV (réel + calés + prévision)],
			        [Heures ouverture théoriques]
			    ),
			    BLANK()
			)
		lineageTag: 413a3e8d-8d4a-40e5-bb00-370cecbbd36c

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Tx remplissage (réel + prévision 6M)' =
			
			VAR Pivot = [Date Pivot (mois terminé)]
			VAR d = MAX(Calendrier[Date])
			RETURN
			IF(
			    d <= EOMONTH(Pivot, 6),
			    DIVIDE(
			        [Heures RDV (réel + calés + prévision)],
			        [Heures ouverture théoriques]
			    ),
			    BLANK()
			)
		lineageTag: 194fd7bd-4881-4d6c-8705-a96d0add8a31

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Date Pivot (mois terminé)' =
			
			EOMONTH(TODAY(), -1)
		formatString: General Date
		lineageTag: cda64bea-3b04-449f-b11d-740ddd27376f

	column Placeholder
		isHidden
		formatString: 0
		lineageTag: eb838ea6-440d-4f28-ab20-681de9989c74
		summarizeBy: sum
		isNameInferred
		sourceColumn: [Placeholder]

		annotation SummarizationSetBy = Automatic

	partition Mesures = calculated
		mode: import
		source = Row("Placeholder", BLANK())

	annotation PBI_Id = b61b37aa52d94a28aea5cd28dba662c4

	annotation 436ba87b-9c83-4389-a31b-ebd06a36be98 = {"Expression":""}

